// ------------------------------
// Friendship Script
// ------------------------------

//Levelling

IF
  InParty(Myself)
  Global("TILiriaJoined","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobal("TILiriaJoined","GLOBAL",1)
END

IF
  InParty(Myself)
  Global("TILiriaJoined","GLOBAL",1)
  Global("TILiriaLeveled","GLOBAL",0)
  XPLT(Player1,1500)
THEN
  RESPONSE #100
    SetGlobal("TILiriaLeveled","GLOBAL",1)
END

IF
  InParty(Myself)
  Global("TILiriaJoined","GLOBAL",1)
  Global("TILiriaLeveled","GLOBAL",0)
  XPGT(Player1,1499)
  XPLT(Player1,3000)
THEN
  RESPONSE #100
    SetGlobal("TILiriaLeveled","GLOBAL",1)
    AddXPObject("TILiria",2251) // level 2
END

IF
  InParty(Myself)
  Global("TILiriaJoined","GLOBAL",1)
  Global("TILiriaLeveled","GLOBAL",0)
  XPGT(Player1,2999)
  XPLT(Player1,6000)
THEN
  RESPONSE #100
    SetGlobal("TILiriaLeveled","GLOBAL",1)
    AddXPObject("TILiria",4501) // level 3
END

IF
  InParty(Myself)
  Global("TILiriaJoined","GLOBAL",1)
  Global("TILiriaLeveled","GLOBAL",0)
  XPGT(Player1,5999)
  XPLT(Player1,13000)
THEN
  RESPONSE #100
    SetGlobal("TILiriaLeveled","GLOBAL",1)
    AddXPObject("TILiria",9001) // level 4
END

IF
  InParty(Myself)
  Global("TILiriaJoined","GLOBAL",1)
  Global("TILiriaLeveled","GLOBAL",0)
  XPGT(Player1,12999)
  XPLT(Player1,27500)
THEN
  RESPONSE #100
    SetGlobal("TILiriaLeveled","GLOBAL",1)
    AddXPObject("TILiria",18001) // level 5
END

IF
  InParty(Myself)
  Global("TILiriaJoined","GLOBAL",1)
  Global("TILiriaLeveled","GLOBAL",0)
  XPGT(Player1,27499)
  XPLT(Player1,55000)
THEN
  RESPONSE #100
    SetGlobal("TILiriaLeveled","GLOBAL",1)
    AddXPObject("TILiria",36001) // level 6
END

IF
  InParty(Myself)
  Global("TILiriaJoined","GLOBAL",1)
  Global("TILiriaLeveled","GLOBAL",0)
  XPGT(Player1,54999)
  XPLT(Player1,110000)
THEN
  RESPONSE #100
    SetGlobal("TILiriaLeveled","GLOBAL",1)
    AddXPObject("TILiria",75001) // level 7
END

IF
  InParty(Myself)
  Global("TILiriaJoined","GLOBAL",1)
  Global("TILiriaLeveled","GLOBAL",0)
  XPGT(Player1,109999)
THEN
  RESPONSE #100
    SetGlobal("TILiriaLeveled","GLOBAL",1)
    AddXPObject("TILiria",150001) // level 8
END

//Set Timers

IF
    InParty(Myself)
    Global("TILiriaShutUp","GLOBAL",0)
    Global("TILiriaTalks","GLOBAL",0)
THEN
  RESPONSE #100
    RealSetGlobalTimer("TILiriaTalksTimer","GLOBAL",600)
    SetGlobal("TILiriaTalks","GLOBAL",1)
END

//Romance/Friend Check

//TILiriaCheck Romance

IF
    InParty(Myself)
    Global("TILiriaRomanceMatch","GLOBAL",0)
    Global("TILiriaRomanceCheck","GLOBAL",0)
    Global("TILiriaLoveTalks","GLOBAL",0)
    Alignment(Player1,MASK_GOOD)
    CheckStatGT(Player1,13,CHR)
    CheckStatGT(Player1,13,STR)
      !RACE (Player1, HALFORC)
    ReputationGT(Player1,11)
THEN
  RESPONSE #100
    SetGlobal("TILiriaLoveTalks","GLOBAL",1)
    SetGlobal("TILiriaRomanceMatch","GLOBAL",1)
    SetGlobal("TILiriaRomanceCheck","GLOBAL",1)
END

//TILiriaCheck Friend

IF
    InParty(Myself)
    Global("TILiriaFriendMatch","GLOBAL",0)
    Global("TILiriaFriendCheck","GLOBAL",0)
    OR (3)
    Alignment(Player1,MASK_GOOD)
    Alignment(Player1,MASK_GENEUTRAL)
    Alignment(Player1,MASK_LCNEUTRAL)
    CheckStatGT(Player1,11,CHR)
    CheckStatGT(Player1,11,STR)
    ReputationGT(Player1,8)
THEN
  RESPONSE #100
    SetGlobal("TILiriaFriendMatch","GLOBAL",1)
    SetGlobal("TILiriaFriendCheck","GLOBAL",1)
END


//TILiriaCheck Fail

IF
    InParty(Myself)
    Global("TILiriaFriendMatch","GLOBAL",0)
    Global("TILiriaFriendCheck","GLOBAL",0)
    Global("TILiriaRomanceMatch","GLOBAL",0)
    Global("TILiriaRomanceCheck","GLOBAL",0)
    OR(4)
       RACE (Player1, HALFORC)
    !Alignment(Player1,MASK_GOOD)
    !Alignment(Player1,MASK_GENEUTRAL)
    !Alignment(Player1,MASK_LCNEUTRAL)
THEN
    RESPONSE #100
      SetGlobal("TIMartaRomanceMatch","GLOBAL",0)
      SetGlobal("TIMartaRomanceCheck","GLOBAL",1)
      SetGlobal("TILiriaFriendMatch","GLOBAL",0)
      SetGlobal("TILiriaFriendCheck","GLOBAL",1)

  END

// Triggering talks
IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    RealGlobalTimerExpired("TILiriaFriendTalkTimer","GLOBAL")
    CombatCounter(0)
    !See([ENEMY])
    !Global("TILiriaFriendship","GLOBAL",3)
    OR(14)
      Global("TILiriaFriendTalk","GLOBAL",2)
      Global("TILiriaFriendTalk","GLOBAL",6)
      Global("TILiriaFriendTalk","GLOBAL",8)
      Global("TILiriaFriendTalk","GLOBAL",10)
      Global("TILiriaFriendTalk","GLOBAL",12)
      Global("TILiriaFriendTalk","GLOBAL",14)
      Global("TILiriaFriendTalk","GLOBAL",16)
      Global("TILiriaFriendTalk","GLOBAL",18)
      Global("TILiriaFriendTalk","GLOBAL",20)
      Global("TILiriaFriendTalk","GLOBAL",24)
      Global("TILiriaFriendTalk","GLOBAL",28)
      Global("TILiriaFriendTalk","GLOBAL",30)
      Global("TILiriaFriendTalk","GLOBAL",32)
      Global("TILiriaFriendTalk","GLOBAL",34)
THEN
  RESPONSE #100
    MoveToObject(Player1)
    Dialogue(Player1)
END

IF
    InParty(Myself)
    See(Player1)
    RealGlobalTimerExpired("TILiriaFriendTalkTimer","GLOBAL")
    !Global("TILiriaFriendship","GLOBAL",3)
    OR(11)
      Global("TILiriaFriendTalk","GLOBAL",1)
      Global("TILiriaFriendTalk","GLOBAL",5)
      Global("TILiriaFriendTalk","GLOBAL",7)
      Global("TILiriaFriendTalk","GLOBAL",9)
      Global("TILiriaFriendTalk","GLOBAL",13)
      Global("TILiriaFriendTalk","GLOBAL",15)
      Global("TILiriaFriendTalk","GLOBAL",17)
      Global("TILiriaFriendTalk","GLOBAL",23)
      Global("TILiriaFriendTalk","GLOBAL",27)
      Global("TILiriaFriendTalk","GLOBAL",31)
      Global("TILiriaFriendTalk","GLOBAL",33)
THEN
  RESPONSE #100
    IncrementGlobal("TILiriaFriendTalk","GLOBAL",1)
END


// FT 6: after bandit camp
IF
    InParty(Myself)
    !Dead("TILiria")
    GlobalGT("Chapter","GLOBAL",%tutu_chapter_3%)
    Global("TILiriaFriendTalk","GLOBAL",11)
    !Global("TILiriaFriendship","GLOBAL",3)
    Global("TILiriaBandits","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobal("TILiriaBandits","GLOBAL",1)
    RealSetGlobalTimer("TILiriaFriendTalkTimer","GLOBAL",2700)
END

IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    RealGlobalTimerExpired("TILiriaFriendTalkTimer","GLOBAL")
    CombatCounter(0)
    !See([ENEMY])
    !Global("TILiriaFriendship","GLOBAL",3)
    Global("TILiriaFriendTalk","GLOBAL",11)
    Global("TILiriaBandits","GLOBAL",1)
THEN
  RESPONSE #100
    IncrementGlobal("TILiriaFriendTalk","GLOBAL",1)
END

// FT 10: After clearing cloakwood
IF
    InParty(Myself)
    !Dead("TILiria")
    GlobalGT("Chapter","GLOBAL",%tutu_chapter_4%)
    Global("TILiriaFriendTalk","GLOBAL",19)
    !Global("TILiriaFriendship","GLOBAL",3)
    Global("TILiriaCloakwood","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobal("TILiriaCloakwood","GLOBAL",1)
    RealSetGlobalTimer("TILiriaFriendTalkTimer","GLOBAL",2700)
END

IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    RealGlobalTimerExpired("TILiriaFriendTalkTimer","GLOBAL")
    CombatCounter(0)
    !See([ENEMY])
    !Global("TILiriaFriendship","GLOBAL",3)
    Global("TILiriaFriendTalk","GLOBAL",19)
    Global("TILiriaCloakwood","GLOBAL",1)
THEN
  RESPONSE #100
    IncrementGlobal("TILiriaFriendTalk","GLOBAL",1)
END

// FT 15
IF
    InParty(Myself)
    !Dead("TILiria")
    Global("TILiriaFriendTalk","GLOBAL",29)
    !Global("TILiriaFriendship","GLOBAL",3)
    GlobalGT("Chapter","GLOBAL",%tutu_chapter_6%)
    Global("TILiriaCandlekeep","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobal("TILiriaCandlekeep","GLOBAL",1)
    RealSetGlobalTimer("TILiriaFriendTalkTimer","GLOBAL",600)
END

IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    RealGlobalTimerExpired("TILiriaFriendTalkTimer","GLOBAL")
    CombatCounter(0)
    !See([ENEMY])
    !Global("TILiriaFriendship","GLOBAL",3)
    Global("TILiriaFriendTalk","GLOBAL",29)
    Global("TILiriaCandlekeep","GLOBAL",1)
THEN
  RESPONSE #100
    IncrementGlobal("TILiriaFriendTalk","GLOBAL",1)
END

//Area: %Temple_SongoftheMorning%
IF 
    InParty(Myself)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    Global("TILiria_temple","GLOBAL",0)
    AreaCheck("%Temple_SongoftheMorning%")
THEN
  RESPONSE #100
    SetGlobal("TILiria_temple","GLOBAL",1)
END

//Area: %NashkelMines_L1%
IF
    InParty(Myself)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    Global("TILiria_mines","GLOBAL",0)
    AreaCheck("%NashkelMines_L1%")
THEN
  RESPONSE #100
    SetGlobal("TILiria_mines","GLOBAL",1)
END

//Area %ValleyoftheTombs_Tomb%
IF
    InParty(Myself)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    Global("TILiria_Valley","GLOBAL",0)
    AreaCheck("%ValleyoftheTombs_Tomb%")
THEN
  RESPONSE #100
    SetGlobal("TILiria_Valley","GLOBAL",1)
END

//Area %CloakwoodMines_L4% sub area: shrine to Bhaal (room NW of bedroom adjacent to elevator)
/*
IF
    InParty(Myself)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    Global("TILiria_bhaal","GLOBAL",1)
    AreaCheck("%CloakwoodMines_L4%")
THEN
  RESPONSE #100
    SetGlobal("TILiria_bhaal","GLOBAL",2)
END  */

//(If you flood the mine without freeing the slaves)
IF
    InParty(Myself)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    Global("TILiria_flood","GLOBAL",0)
    !Global("FREESLAV","GLOBAL",1)
    GlobalGT("FLOODED","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobal("TILiria_flood","GLOBAL",1)
END

//Area %Candlekeep_Library_L1%
IF
    InParty(Myself)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    Global("TILiria_candlekeep","GLOBAL",0)
    AreaCheck("%Candlekeep_Library_L1%")
THEN
  RESPONSE #100
    SetGlobal("TILiria_candlekeep","GLOBAL",1)
END

//(When Charname finds Gorion's letter - Shadowkeeper gave me an error when looking up the item code, and Tutu triggers a dialogue when you pick up the letter, and only if Global("TILiriaFriendship","GLOBAL",1))
IF
    InParty(Myself)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    Global("TILiria_letter","GLOBAL",0)
    Global("TILiriaFriendship","GLOBAL",1)
    PartyHasItem("%tutu_var%SCRL2J")
THEN
  RESPONSE #100
    SetGlobal("TILiria_letter","GLOBAL",1)
END

//In inventory: POTN48.itm
IF
    InParty(Myself)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    Global("TILiria_potion","GLOBAL",0)
    PartyHasItem("POTN48")
THEN
  RESPONSE #100
    SetGlobal("TILiria_potion","GLOBAL",1)
END

//In inventory: BOOK03.itm, BOOK04.itm, BOOK05.itm, BOOK06.itm BOOK07.itm, or BOOK08.itm. Should only fire once regardless of which book you put in.
IF
    InParty(Myself)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    Global("TILiria_book","GLOBAL",0)
    OR(6)
      PartyHasItem("BOOK03")
      PartyHasItem("BOOK04")
      PartyHasItem("BOOK05")
      PartyHasItem("BOOK06")
      PartyHasItem("BOOK07")
      PartyHasItem("BOOK08")
THEN
  RESPONSE #100
    SetGlobal("TILiria_book","GLOBAL",1)
END


// Triggering talks
IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
    OR(9)
      Global("TILiria_temple","GLOBAL",1)
      Global("TILiria_mines","GLOBAL",1)
      Global("TILiria_Lyrley","GLOBAL",1)
      Global("TILiria_bhaal","GLOBAL",2)
      Global("TILiria_flood","GLOBAL",1)
      Global("TILiria_candlekeep","GLOBAL",1)
      Global("TILiria_letter","GLOBAL",1)
      Global("TILiria_potion","GLOBAL",1)
      Global("TILiria_book","GLOBAL",1)
THEN
  RESPONSE #100
    MoveToObject(Player1)
    Dialogue(Player1)
END

